plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'jacoco'
    id "com.github.node-gradle.node" version "7.0.2"
}

group = 'org.hyunggi'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

jar {
    enabled = false
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    asciidoctorExt
}

repositories {
    mavenCentral()
}

dependencies {
    //Spring web
    implementation 'org.springframework.boot:spring-boot-starter-web'

    //Spring Actuator
    //implementation 'org.springframework.boot:spring-boot-starter-actuator'

    //Spring Jpa
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    //P6Spy [설정 : application.yml(decorator.datasource.p6spy.enable-logging : true)]
    //implementation("com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.9.1")

    //Spring Validation
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    //Spring Security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.security:spring-security-test'

    //AWS Parameter Store
    implementation platform("io.awspring.cloud:spring-cloud-aws-dependencies:3.1.0")
    implementation 'io.awspring.cloud:spring-cloud-aws-starter-parameter-store'

    //H2
    runtimeOnly 'com.h2database:h2'

    //MySql Connector
    runtimeOnly 'com.mysql:mysql-connector-j'

    //Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    //Lombok (테스트 의존성 추가)
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    //테스트 코드
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    //RestDocs
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

    //JWT 사용
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'
}

ext { // 전역 변수
    snippetsDir = file('build/generated-snippets')
}

test {
    outputs.dir snippetsDir
    finalizedBy jacocoTestReport
    useJUnitPlatform()
}

jacoco {
    toolVersion = "0.8.11"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required.set(true)
    }
}

asciidoctor {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'

    sources { // 특정 파일만 html로 만든다.
        include('**/index.adoc')
    }
    baseDirFollowsSourceFile() // 다른 adoc 파일을 include 할 때 경로를 baseDir로 맞춘다.
    dependsOn test
}

bootJar {
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}") {
        into 'static/docs'
    }
}

node {
    download = true
    version = '20.11.0'
    distBaseUrl = "https://nodejs.org/dist"
    npmInstallCommand = "install"
    workDir = file("${project.projectDir}/.gradle/nodejs")
    nodeProjectDir = file("${project.projectDir}/../my-garden-fe")
}

task deleteOldVueFiles(type: Delete) {
    delete "src/main/resources/static"
}

task npmBuildWithoutInstall(type: NpmTask, dependsOn: ['deleteOldVueFiles']) {
    args = ["run", "build"]
}

task npmBuildWithInstall(type: NpmTask, dependsOn: ['deleteOldVueFiles', 'npmInstall']) {
    args = ["run", "build"]
}

// 환경 변수 또는 프로젝트 속성을 확인하여 조건부로 태스크 의존성 설정
if (System.getenv('NPM_BUILD_CACHE') == 'true') {
    processResources.dependsOn 'npmBuildWithoutInstall'
} else {
    processResources.dependsOn 'npmBuildWithInstall'
}
